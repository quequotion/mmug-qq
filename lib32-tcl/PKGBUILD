# $Id$
# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=lib32-tcl
pkgver=8.6.3
pkgrel=1
pkgdesc="The Tcl scripting language (32-bit)"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('lib32-zlib')
provides=("lib32-tcl=$pkgver")
options=('staticlibs')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
md5sums=('db382feca91754b7f93da16dc4cdad1f')

prepare() {
  cd tcl${pkgver}
  # we build the tcl sqlite interface in sqlite-tcl package
  rm -rf pkgs/sqlite3*
}

build() {
  cd tcl${pkgver}/unix
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS+=" -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

#  [[ $CARCH == "x86_64" ]] && BIT="--enable-64bit"
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32  --includedir=/usr/lib32/tcl/include \
              --mandir=/usr/share/man --enable-threads #$BIT

  make
}

check() {
  cd tcl${pkgver}/unix
  make test
}

package() {
  cd tcl${pkgver}/unix
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf tclsh${pkgver%.*} "${pkgdir}/usr/bin/tclsh"
  ln -sf libtcl${pkgver%.*}.so "${pkgdir}/usr/lib32/libtcl.so"

  # remove buildroot traces
  sed -e "s#${srcdir}/tcl${pkgver}/unix#/usr/lib32#" \
      -e "s#${srcdir}/tcl${pkgver}#/usr/lib32/tcl/include#" \
      -i "${pkgdir}/usr/lib32/tclConfig.sh"

  sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/tdbc1.0.2#/usr/lib32/tdbc1.0.2#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.2/generic#/usr/lib32/tcl/include#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.2/library#/usr/lib32/tcl${pkgver%.*}#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.2#/usr/lib32/tcl/include#" \
      -i "${pkgdir}/usr/lib32/tdbc1.0.2/tdbcConfig.sh"

  sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/itcl4.0.2#/usr/lib32/itcl4.0.2#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/itcl4.0.2/generic#/usr/lib32/tcl/include#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/itcl4.0.2#/usr/lib32/tcl/include#" \
      -i "${pkgdir}/usr/lib32/itcl4.0.2/itclConfig.sh"

  # remove 64-bit and unarched stuff
  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/ ?

  install -Dm644 ../license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

