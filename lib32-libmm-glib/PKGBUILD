# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=lib32-modemmanager
pkgname=(lib32-modemmanager lib32-libmm-glib)
pkgver=1.2.0
pkgrel=4
pkgdesc="Mobile broadband modem management service (32bit)"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2 LGPL2.1)
depends=(lib32-systemd polkit ppp lib32-libqmi lib32-libmbim)
makedepends=(intltool gtk-doc gobject-introspection vala)
source=(http://www.freedesktop.org/software/ModemManager/ModemManager-$pkgver.tar.xz)
sha256sums=('8bd9d8c3b506f34a9f927e95784026d1fe132c9d60fc426a892b690f7d6408bc')

build() {
  cd ModemManager-$pkgver

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --with-udev-base-dir=/usr/lib/udev \
        --with-polkit=permissive \
        --enable-gtk-doc \
        --disable-static
  make
}

package_lib32-modemmanager() {
  depends+=(libmm-glib)
  optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
  options=(!emptydirs)

  cd ModemManager-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libmm-glib uninstall
  make DESTDIR="$pkgdir" -C vapi uninstall

  # Some stuff to move is left over
  mv "$pkgdir/usr/include" ..
  mv "$pkgdir/usr/lib32/pkgconfig" ..

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/
}

package_lib32-libmm-glib() {
  pkgdesc="ModemManager library"
  depends=(glib2)

  install -d "$pkgdir/usr/lib32"
  mv include "$pkgdir/usr"
  mv pkgconfig "$pkgdir/usr/lib32"

  cd ModemManager-$pkgver
  make DESTDIR="$pkgdir" -C libmm-glib install
  make DESTDIR="$pkgdir" -C vapi install

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin}}
}

