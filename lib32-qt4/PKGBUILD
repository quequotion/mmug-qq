# $Id: PKGBUILD 80785 2012-12-03 16:17:19Z bluewind $
# Maintainer: Florian Pritz <flo@xssn.at>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_pkgbasename=qt4
pkgname=lib32-$_pkgbasename
pkgver=4.8.6
pkgrel=4
pkgdesc='A cross-platform application and UI framework (32-bit)'
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL')
depends=(lib32-{fontconfig,sqlite3,alsa-lib,glib2,libdbus,openssl}
         lib32-lib{png,tiff,mng,gl,sm,xrandr,xv,xi} $_pkgbasename)
optdepends=('lib32-libxinerama: Xinerama support'
            'lib32-libxcursor: Xcursor support'
            'lib32-libxfixes: Xfixes support')
makedepends=(cups gcc-multilib lib32-{mesa,libcups,libxfixes,gtk2})
options=('staticlibs') # libQtUiTools builds as static only FS#36606
provides=('lib32-qtwebkit')
replaces=('lib32-qtwebkit' 'lib32-qt<=4.8.4')
conflicts=('lib32-qtwebkit' 'lib32-qt')
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/4.8/${pkgver}/${_pkgfqn}.tar.gz"
        'find-32bit-mysql.h.patch')
sha512sums=('c2d07c3cf9d687cb9b93e337c89df3f0055bd02bc8aa5ecd55d3ffb238b31a4308aeabc3c51a4f94ac76a1b00796f047513d02e427ed93ae8dd99f836fff7692'
            '6cd5d4f579d0ec5b8d52239473477e86fa2d6758fbb59352b6ab0eda7c2e9a74674ee327963df252a5e0fcbac1662b5c96e1625c8adb93a5b45da9c277649d1a')

prepare() {
  cd $srcdir/$_pkgfqn

  export QT4DIR=$srcdir/$_pkgfqn
  export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # some of those are likely unnecessary, but I'm too lazy to find and remove them
  sed -i "/^QMAKE_LINK\s/s|g++|g++ -m32|g" mkspecs/common/g++-base.conf
  sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/g++-base.conf
  sed -i "s|-O2|${CXXFLAGS} -m32|" mkspecs/common/gcc-base.conf
  sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS} -m32|g" mkspecs/common/gcc-base.conf
  sed -i "s|-Wl,-O1|-m32 -Wl,-O1|" mkspecs/common/g++-unix.conf
  sed -e "s|-O2|$CXXFLAGS -m32|" \
      -e "/^QMAKE_RPATH/s| -Wl,-rpath,||g" \
      -e "/^QMAKE_LINK\s/s|g++|g++ -m32|g" \
      -e "/^QMAKE_LFLAGS\s/s|+=|+= $LDFLAGS|g" \
      -i mkspecs/common/g++.conf

  #This has to be forced.
  patch -Np2 < ../find-32bit-mysql.h.patch
}

build() {
  cd $srcdir/$_pkgfqn
  export QT4DIR=$srcdir/$_pkgfqn
  export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS+=" -m32 -L/usr/lib32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure -v -confirm-license -opensource -platform linux-g++-32 \
    -prefix /usr \
    -libdir /usr/lib32/qt4/ \
    -bindir /usr/lib32/qt4/bin \
    -headerdir /usr/lib32/qt4/include \
    -docdir /usr/share/doc/qt4 \
    -plugindir /usr/lib32/qt4/plugins \
    -importdir /usr/lib32/qt4/imports \
    -datadir /usr/share/qt4 \
    -translationdir /usr/share/qt4/translations \
    -sysconfdir /etc \
    -plugin-sql-{psql,mysql,sqlite,odbc,ibase} \
    -system-sqlite \
    -no-phonon \
    -no-phonon-backend \
    -webkit \
    -graphicssystem raster \
    -openssl-linked \
    -nomake demos \
    -nomake examples \
    -nomake docs \
    -no-rpath \
    -optimized-qmake \
    -dbus-linked \
    -reduce-relocations \
    -no-openvg

  make
}

package() {
  cd $srcdir/$_pkgfqn
  make INSTALL_ROOT=$pkgdir install

  rm -rf "${pkgdir}"/usr/{include,share,bin,tests}
  install -Dd "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
  install -Dd "$pkgdir/usr/lib32/pkgconfig"
  ln -s "$pkgdir"/usr/lib32/qt4/pkgconfig/* "$pkgdir"/usr/lib32/pkgconfig/

}

