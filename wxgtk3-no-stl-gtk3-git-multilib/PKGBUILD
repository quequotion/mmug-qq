# Maintainer: Que Quotion <quequotion@bugmenot.com>
# Maintainer: Samuel Mesa <samuelmesa@linuxmail.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=wxgtk-gtk3-no-stl-no-sdl-svn-multilib
pkgname=({lib32-,}wxgtk-gtk3-no-stl-no-sdl-svn)
pkgver=3.0.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
makedepends=('gconf')
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
options=('!emptydirs')
source=("svn+https://svn.wxwidgets.org/svn/wx/wxWidgets/trunk"
        'wxGTK-collision.patch'
        'wx-gdk-wayland.patch')
sha512sums=('SKIP'
            'bb552f481f575c164d5e9f2c0937952568bfdba7efe87fffa1182749bdce7780dd5a26c4b4a6df817be58ad6c3a49a76b1effd993a4896c3c380a02f38a669fc'
            '95a9e10aec17dab62256f941dd32ceab07e081eae40ba6fe99cc7ea4ef3ed6d042a215c1763fdd2f08fa51ad4b7d7fa17577914893f2a8dcb6ffc37be5271997')

pkgver() {
  cd "$pkgname"
  local ver="$(svnversion)"
  printf "3.0.1r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd trunk

  patch -Np1 -i ${srcdir}/wx-gdk-wayland.patch
}

build() {

  cd trunk

  #Build 64bit libraries
  [[ -d build-x86_64 ]] || mkdir build-x86_64
  pushd build-x86_64


  ../configure --prefix=/usr --libdir=/usr/lib \
              --enable-{graphics_ctx,mediactrl,unicode,webview} \
              --with-lib{jpeg,png,tiff,xpm}=sys --with-regex=builtin --with-opengl --with-gtk=3 --with-sdl=no \
              --disable-{precomp-headers,stc} # stc is broken
  make #-j1
  make -C ../locale allmo

  popd

  #Build 32bit libraries
  patch -Np1 < ${srcdir}/wxGTK-collision.patch

  [[ -d build-i686 ]] || mkdir build-i686
  pushd build-i686

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ../configure --prefix=/usr --libdir=/usr/lib32 --includedir=/usr/lib32/wx/ \
              --enable-{graphics_ctx,mediactrl,unicode,webview} \
              --with-lib{jpeg,png,tiff,xpm}=sys --with-regex=builtin --with-opengl --with-gtk=3 --with-sdl=no \
              --disable-{precomp-headers,stc} # stc is broken
  make #-j1
  make -C ../locale allmo

  popd
}

package_wxgtk-gtk3-no-stl-no-sdl-svn() {
  depends=('gtk3' 'gstreamer0.10-base' 'libnotify' 'libsm')
  makedepends+=('gstreamer0.10-base-plugins' 'glu' 'webkitgtk')
  provides=(wxgtk{,3}{,-gtk3}{,-no-stl}{,-no-sdl}{,-svn}=$pkgver)
  conflicts=('wxgtk')

  cd "${srcdir}/trunk/build-x86_64"

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_lib32-wxgtk-gtk3-no-stl-no-sdl-svn() {
  pkgdesc+=" (32bit)"
  depends=(lib32-{gtk3,gstreamer0.10-base,libnotify,libsm})
  makedepends+=(lib32-{gstreamer0.10-base-plugins,glu,webkitgtk} 'gcc-multilib')
  provides=(lib32-wxgtk{,3}{,-gtk3}{,-no-stl}{,-no-sdl}{,-svn}=$pkgver)
  conflicts=('lib32-wxgtk')

  cd "${srcdir}/trunk/build-i686"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/{share,include}} # needs bin/

  install -dm 755 "${pkgdir}"/usr/share/licenses
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
