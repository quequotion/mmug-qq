# Maintainer: Benjamin Bukowski <benjamin.bukowski@gmail.com>

pkgname=lib32-libfbclient
_pkgver=2.5.3.26778
_buildver=0
pkgver=${_pkgver}_${_buildver}
pkgrel=1
pkgdesc="Client library for Firebird. (32-Bit)"
arch=('x86_64')
url="http://www.firebirdsql.org/"
license=('IPL' 'IDPL')
depends=('lib32-glibc>=2.7' 'lib32-gcc-libs' 'lib32-icu>=50.1' 'lib32-ncurses')
makedepends=('gcc-multilib')
options=('!makeflags' '!libtool')
source=(http://downloads.sourceforge.net/firebird/Firebird-${_pkgver}-${_buildver}.tar.bz2)
md5sums=('5feff765e11a5fb0ec17c71e155bb974')

build() {
  cd $srcdir/Firebird-${_pkgver}-${_buildver}

	export CC="gcc -m32"
	export CXX="g++ -m32"
        export LDFLAGS='-m32'
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32 --includedir=/usr/lib32/firebird \
    --with-system-icu --without-fbudf --without-fbsbin --without-fbconf \
    --without-fbdoc --without-fbsample --without-fbsample-db --without-fbintl \
    --without-fbmisc --without-fbhelp --without-fbsecure-db --with-fbmsg=/usr/share/firebird \
    --without-fblog --without-fbglock --without-fbplugins

  make
}

package() {
  cd $srcdir/Firebird-${_pkgver}-${_buildver}

  install -Dd "${pkgdir}/usr/lib32/firebird/include"
  cp -R gen/firebird/lib/* $pkgdir/usr/lib32
  cp -R gen/firebird/include $pkgdir/usr/lib32/firebird
  rm -f $pkgdir/usr/lib32/libedit.a

  # Add libgds support FS#30062 FS#30282
  cd $pkgdir/usr/lib32
  ln -s libfbclient.so libgds.so.0
  ln -s libfbclient.so libgds.so

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin}}
}

