# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Daniel Kirchner <daniel at ekpyron dot rog>
# Contributor: Gustavo Alvarez <sl1pkn07 at gmail dot com>
# Contributor: Chase Geigle <sky at skystrife dot com>
# Contributor: Axper Jan <483ken at gmail dot com>

pkgbase=sdl2-hg-multilib
pkgname=({lib32-,}sdl2-hg)
pkgver=2.0.3.r681.47a5c5dae9b6
pkgrel=1
pkgdesc="A library for portable low-level access to video, audio and input (development version)"
depends=('sh')
makedepends=('mercurial' 'cmake')
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('ZLIB')
source=(sdl2::"hg+http://hg.libsdl.org/SDL"
        'lib32-sdl2.patch'
        {gcc,ld}-flagged)
md5sums=('SKIP'
         '7541c3ccb1955c0855f85e1bbbea819d'
         '1cb09d4c1569e261f21a03fc2e08e5ac'
         '4e16a96b05c986bc940e8aef595d5cb6')

pkgver() {
  cd sdl2
  _tag=$(hg tags -q | sort -r | grep release- | head -n1)
  _commits=$(hg log --template "{node}\n" -r $_tag:tip | wc -l)
  echo "${_tag/release-}.r$_commits.$(hg identify -i)"
}

prepare() {
  #cmake has no honor, so decieve it with tricks!
  chmod +x {gcc,ld}-flagged
}

build() {

  #Build 64bit libraries
  [[ -d build-x86_64 ]] || mkdir build-x86_64
  pushd build-x86_64

  cmake ../sdl2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -D{ARTS,NAS,ESD,SDL_STATIC,RPATH,VIDEO_WAYLAND}=OFF \
    -DCMAKE_C_COMPILER="$srcdir"/gcc-flagged \
    -DCMAKE_LINKER="$srcdir"/ld-flagged
  make

  popd

  #Build 32bit libraries

  #patch -Np1 < ../lib32-sdl2.patch

  [[ -d build-i686 ]] || mkdir build-i686
  pushd build-i686

  export EXEEXT='-32'

  CFLAGS="$CFLAGS -m32"
  LDFLAGS="$LDFLAGS -m32"

  cmake ../sdl2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -D{ARTS,NAS,ESD,SDL_STATIC,RPATH,VIDEO_WAYLAND}=OFF \
    -DCMAKE_PREFIX_PATH=/usr/lib32 \
    -DLIB_SUFFIX='32' \
    -DCMAKE_C_COMPILER="$srcdir"/gcc-flagged \
    -DCMAKE_LINKER="$srcdir"/ld-flagged 

  make

  popd
}

package_sdl2-hg() {
  depends+=('libxext' 'libxrender' 'libx11' 'libgl' 'libxcursor')
  makedepends+=('alsa-lib' 'mesa' 'libpulse' 'libxrandr' 'libxinerama' 'libxkbcommon')
  optdepends=('alsa-lib: ALSA audio driver'
              'libpulse: PulseAudio audio driver')
  provides=(sdl2{,-hg}=$pkgver)
  conflicts=('sdl2')
  make -C build-x86_64 DESTDIR="$pkgdir/" install

  install -Dm644 sdl2/COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_lib32-sdl2-hg() {
  pkgdesc+=" (32bit)"
  depends+=(lib32-{libxext,libxrender,libx11,libgl,libxcursor})
  makedepends+=(lib32-{alsa-lib,mesa,libpulse,libxrandr,libxinerama,libxkbcommon} 'gcc-multilib')
  optdepends=('lib32-alsa-lib: ALSA audio driver'
              'lib32-libpulse: PulseAudio audio driver')
  provides=(lib32-sdl2{,-hg}=$pkgver)
  conflicts=('lib32-sdl2')
  make -C build-i686 DESTDIR="$pkgdir/" install

  mv "$pkgdir"/usr/bin/sdl2-config "$pkgdir"/usr/bin/sdl2-config-32

  rm -rf "${pkgdir}"/{etc,usr/{share,include}} # needs bin/

  install -Dm644 sdl2/COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
