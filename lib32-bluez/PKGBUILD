# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

pkgbase=lib32-bluez
pkgname=('lib32-bluez' 'lib32-bluez-utils' 'lib32-bluez-libs' 'lib32-bluez-cups' 'lib32-bluez-hid2hci' 'lib32-bluez-plugins')
pkgver=5.21
pkgrel=2
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('lib32-dbus' 'lib32-libical' 'lib32-systemd')
source=(http://www.kernel.org/pub/linux/bluetooth/bluez-${pkgver}.tar.xz
        0001-Allow-using-obexd-without-systemd-in-the-user-session.patch
        bluetooth.modprobe)
# see https://www.kernel.org/pub/linux/bluetooth/sha256sums.asc
sha256sums=('81eb073516f39ff1fa02fa40120e834f40fdb0b772f8f4153a9ab566cc0a1f4f'
            'd7bc2ed3552edfb18380c83792d8d86efa20f5bada4be33566d782cbcb7fe231'
            '69d68d649daebb236c6482a8398cbc04b826d4a4d51a8850d136a8c6a36baed5')

prepare() {
  cd bluez-$pkgver
  # patch not upstreamable for obex file transfer - FS#37773 taken from FC
  # http://www.hadess.net/2013/11/bluetooth-file-sharing-obexpush-in.html
  patch -Np1 -i ${srcdir}/0001-Allow-using-obexd-without-systemd-in-the-user-session.patch
  autoreconf -vfi
}

build() {

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd bluez-${pkgver}
  ./configure \
          --prefix=/usr --libdir=/usr/lib32 \
          --mandir=/usr/share/man \
          --sysconfdir=/etc \
          --localstatedir=/var \
          --libexecdir=/usr/lib32 \
          --enable-sixaxis \
          --enable-experimental \
          --enable-library # this is deprecated

  make # all # V=1
}

check() {
  cd bluez-$pkgver
  #make check
}


package_lib32-bluez() {
  pkgdesc="Daemons for the bluetooth protocol stack (32bit)"
  depends=('lib32-libical' 'lib32-dbus' 'lib32-glib2')
  backup=('etc/dbus-1/system.d/bluetooth.conf'
          'etc/bluetooth/main.conf')
  conflicts=('obexd-client' 'obexd-server')

  cd bluez-${pkgver}
  #make DESTDIR=${pkgdir} install
  #return 1
  
  make DESTDIR=${pkgdir} \
       install-libexecPROGRAMS \
       install-dbussessionbusDATA \
       install-systemdsystemunitDATA \
       install-systemduserunitDATA \
       install-dbussystembusDATA \
       install-dbusDATA \
       install-man8

  # ship upstream main config file
  install -dm755 ${pkgdir}/etc/bluetooth
  install -Dm644 ${srcdir}/bluez-${pkgver}/src/main.conf ${pkgdir}/etc/bluetooth/main.conf

  # add basic documention
  install -dm755 ${pkgdir}/usr/share/doc/${pkgbase}/dbus-apis
  cp -a doc/*.txt ${pkgdir}/usr/share/doc/${pkgbase}/dbus-apis/
  # fix module loading errors
  install -dm755 ${pkgdir}/usr/lib32/modprobe.d
  install -Dm644 ${srcdir}/bluetooth.modprobe ${pkgdir}/usr/lib32/modprobe.d/bluetooth-usb.conf	

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/

}

package_lib32-bluez-utils() {
  pkgdesc="Development and debugging utilities for the bluetooth protocol stack (32bit)"
  depends=('lib32-dbus' 'lib32-systemd')
  conflicts=('lib32-bluez-hcidump')
  provides=('lib32-bluez-hcidump')
  replaces=('lib32-bluez-hcidump' 'lib32-bluez<=4.101')

  cd bluez-${pkgver}
  make DESTDIR=${pkgdir} \
       install-binPROGRAMS \
       install-man1

  # add experimental BT LE manager - FS#41132
  install -Dm755 ${srcdir}/bluez-${pkgver}/tools/btmgmt ${pkgdir}/usr/bin/btmgmt
       
  # libbluetooth.so* are part of libLTLIBRARIES and binPROGRAMS targets
  #make DESTDIR=${pkgdir} uninstall-libLTLIBRARIES
  #rmdir ${pkgdir}/usr/lib32
  rm -rf ${pkgdir}/usr/lib32
  
  # move the hid2hci man page out
  mv ${pkgdir}/usr/share/man/man1/hid2hci.1 ${srcdir}/

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/

}

package_lib32-bluez-libs() {
  pkgdesc="Deprecated libraries for the bluetooth protocol stack (32bit)"
  depends=('lib32-glibc')
  license=('LGPL2.1')

  cd bluez-${pkgver}
  make DESTDIR=${pkgdir} \
       install-includeHEADERS \
       install-libLTLIBRARIES \
       install-pkgconfigDATA

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/

}

package_lib32-bluez-cups() {
  pkgdesc="CUPS printer backend for Bluetooth printers (32bit)"
  depends=('cups')

  cd bluez-${pkgver}
  make DESTDIR=${pkgdir} install-cupsPROGRAMS

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/

}

package_lib32-bluez-hid2hci() {
  pkgdesc="Put HID proxying bluetooth HCI's into HCI mode (32bit)"
  depends=('lib32-systemd')

  cd bluez-${pkgver}
  make DESTDIR=${pkgdir} \
       install-udevPROGRAMS \
       install-rulesDATA
  
  install -dm755 ${pkgdir}/usr/share/man/man1
  mv ${srcdir}/hid2hci.1 ${pkgdir}/usr/share/man/man1/hid2hci.1

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/

}

package_lib32-bluez-plugins() {
  pkgdesc="bluez plugins (PS3 Sixaxis controller) (32bit)"
  depends=('lib32-systemd')

  cd bluez-${pkgver}
  make DESTDIR=${pkgdir} \
       install-pluginLTLIBRARIES

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} # cruft in lib/

}

