# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=lib32-python
pkgver=3.4.1
pkgrel=1
_pybasever=3.4
pkgdesc="Next generation of the python high-level scripting language (32bit)"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org/"
depends=('lib32-expat' 'lib32-bzip2' 'lib32-gdbm' 'lib32-openssl' 'lib32-libffi' 'lib32-zlib')
makedepends=('lib32-tk' 'lib32-sqlite' 'valgrind-multilib' 'lib32-bluez-libs' 'lib32-mpdecimal')
checkdepends=('gdb')
optdepends=('python-setuptools'
            'python-pip'
            'lib32-sqlite'
            'lib32-mpdecimal: for decimal'
            'lib32-xz: for lzma'
            'lib32-tk: for tkinter')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz
        find-32bit.patch)
sha512sums=('09b6390c07334974f189fb1c90bc2310898aef76661b1cecaa2b4d50001fa3df0fa0c63d3471a5a0dc7f9e08dd559d38d3bc45a6c05a816d8b2ed6b2a20e5fa9'
            'b36cfb962072d480c6f4d1d672616e918d7faba89bc1507dab2b55a71febbbcde2b7fa121b2f12fb2f1e2708589aeafb4c923034a212f604c5e75f9da6df78dd')

prepare() {
  cd Python-${pkgver}

  patch -Np2 < ../find-32bit.patch

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib, libffi, and libmpdec),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
  rm -r Modules/_decimal/libmpdec

  #Fix hard-coded paths
  sed -i "s|/lib/|/lib32/|g" "${srcdir}/Python-${pkgver}/Lib/sysconfig.py"
  sed -i "s|lib/|lib32/|g" "${srcdir}/Python-${pkgver}/Modules/getpath.c"
  sed -i "s|/lib/|/lib32/|g" "${srcdir}/Python-${pkgver}/Lib/distutils/command/install.py"
  sed -i "s|/lib/|/lib32/|g" "${srcdir}/Python-${pkgver}/Makefile.pre.in"

}

build() {
  cd Python-${pkgver}


  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  # Disable bundled pip & setuptools
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
              --includedir=/usr/lib32/python${_pybasever}/include \
              --exec_prefix=/usr/lib32/python${_pybasever}/ \
              --bindir=/usr/bin --sbindir=/usr/sbin \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --with-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi \
              --with-system-libmpdec \
              --without-ensurepip

  # Force /usr/lib to /usr/lib32
  sed -i -e 's#$(prefix)/lib#$(prefix)/lib32#' Makefile

  make
}

#check() {
  # test_site: http://bugs.python.org/issue21535
  # test_urllib2_localnet: domain "blackhole.snakebite.net" not exist ?!

  #  cd Python-${pkgver}
  #  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
  #  TERM=screen \
  #     "${srcdir}/Python-${pkgver}/python" -m test.regrtest -uall -x test_posixpath test_uuid test_site test_urllib2_localnet
#}

package() {
  cd Python-${pkgver}
  make DESTDIR="${pkgdir}" install maninstall

  install -dm644 ${pkgdir}/usr/lib32/python${_pybasever}/config-${_pybasever}m/
  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}m.so \
    "${pkgdir}/usr/lib32/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # Clean-up reference to build directory
  sed -i "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib32/python${_pybasever}/config-${_pybasever}m/Makefile"

  # Clean up
  rm -rf "${pkgdir}"/{etc,usr/{share,include}} # needs bin/

  # Dependants use the binary to find the headers...
  cd "${pkgdir}"/usr/bin
  mv python3.4 python3.4-32
  mv python3.4-config python3.4-32-config
  mv python3.4m python3.4m-32
  mv python3.4m-config python3.4m-32-config
  rm 2to3-3.4 idle3.4 pydoc3.4 pyvenv-3.4 2to3 idle3 pydoc3 python3 python3-config pyvenv

  # License
  install -Dm644 ${srcdir}/Python-${pkgver}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

