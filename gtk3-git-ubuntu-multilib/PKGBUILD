# Maintainer: Que Quotion <quequotion@mailinator.com>
# Contributor: Andrew Crerar <asc9003 [at] rit [dot] edu>
# Contributor: RKA KriK <rka_krik@mail.ru>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: Charles Bos <charlesbos1 AT gmail>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: hbdee <hbdee.arch@gmail.com>

pkgbase=gtk3-git-ubuntu-multilib
pkgname=({,lib32-}gtk3-git-ubuntu)
pkgver=3.17.5.1.r43196.gcb38a2e
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (git version with Ubuntu patches from bzr)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
backup=('etc/gtk-3.0/settings.ini')
options=('!libtool')
depends=('shared-mime-info')
makedepends=('bzr' 'pango')
source=('bzr+lp:~ubuntu-desktop/gtk/ubuntugtk3'
        'git://git.gnome.org/gtk+'
        'repatch-071_fix-installation-of-HTML-images.patch'
        'settings.ini')
sha512sums=('SKIP'
            'SKIP'
            'c3dec88bc1fe45b279539087fc6d8509ecc46e5c61afbd2e4e7c91326929604f085c1f24e986059fbb905906fbbb1c04eaccbebe67ab8e67509f4d5bd386e71f'
            '3ee323c287ad4354d5521f584d06149bbe6bb3c6ed4f409e7544c9ac59892a89d7b52e4b7807e6ed7e3a3e064372dc61422153153f67acb97cc371fe1d8200f2')

pkgver() {
  cd "gtk+"
  echo $(git describe --long --tags | cut -d 'g' -f1)r$(git rev-list HEAD --count).$(git describe --always | sed 's/^.*-//1') | tr -d v | tr - .
}

prepare() {

  cd "$srcdir/ubuntugtk3/debian/patches/"

  while read i; do
    if [[ "${i}" =~ ^# || -z "${i}" || "${i}" == *git* ]]; then
      continue # Skip comments, newlines, and git patches
    #Apply available repatches
    elif [ -f "../../../repatch-${i}" ]; then
        msg "Updating ${i} ..." 
        patch -Ns < "../../../repatch-${i}"
    fi
  done < series

  cd "$srcdir/gtk+"

  #Apply Ubuntu patches
  while read i; do
    if [[ "${i}" =~ ^# || -z "${i}" || "${i}" == *git* ]]; then
      continue # Skip comments, newlines, and git patches
    elif [[ "${i}" == 0001-gtkprogressbar-fix-size-allocation.patch ]] || \
         [[ "${i}" == 0001-Add-style-classes-to-the-title-buttons.patch ]] || \
         [[ "${i}" == 044_tracker_fts.patch ]] || \
         [[ "${i}" == mir-backend.patch ]] || \
         [[ "${i}" == mir-backend-unmerged.patch ]] || \
         [[ "${i}" == restore_filechooser_typeaheadfind ]] || \
         [[ "${i}" == 0001-backport-mir-patches-from-upstream-master.patch ]] || \
         [[ "${i}" == 0001-GtkApplication-Prevent-more-crashes-around-shutdown.patch ]]; then
      continue # Skip reduntant or impossible to repair patches
    else
      msg "Applying ${i} ..."
      patch -Nsp1 < "${srcdir}/ubuntugtk3/debian/patches/${i}"
    fi
  done < "${srcdir}/ubuntugtk3/debian/patches/series"

}

build() {

  cd "gtk+"

  #Build native libraries
  [[ -d build-x86_64 ]] || mkdir build-x86_64
  pushd build-x86_64

  ../autogen.sh --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --disable-schemas-compile \
                --enable-{x11,broadway}-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
  popd

  #Build 32bit libraries
  [[ -d build-i686 ]] || mkdir build-i686
  pushd build-i686

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS+=' -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  GTK_IM_MODULE_FILE="gtk.immodules-32" ../autogen.sh --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --disable-schemas-compile \
                --enable-{x11,broadway}-backend \
                --lib{,exec}dir=/usr/lib32
      
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
  popd
}

package_gtk3-git-ubuntu() {
  depends+=('atk' 'at-spi2-atk' 'cairo' 'colord' 'fontconfig' 
            'gdk-pixbuf2' 'json-glib' 'pango' 'rest'
            {glib2,gobject-introspection,wayland}-git 
            lib{cups,xcursor,xinerama,xrandr,xi,xcomposite,xdamage,xkbcommon})
  makedepends+=('gobject-introspection')
  provides=({gtk3{,-git}{,-ubuntu},gtk-update-icon-cache{,-git}}="${pkgver}")
  conflicts=('gtk3' 'gtk-update-icon-cache')
  install=gtk3-git.install
  cd "gtk+/build-x86_64"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}


package_lib32-gtk3-git-ubuntu() {
  pkgdesc+=" (32bit)"
  depends+=(lib32-{atk,at-spi2-atk,cairo,colord,fontconfig,gdk-pixbuf2,json-glib,pango,rest} 
            lib32-{glib2,gobject-introspection,wayland}-git 
            lib32-lib{cups,xcursor,xinerama,xrandr,xi,xcomposite,xdamage,xkbcommon})
  makedepends+=('lib32-gobject-introspection' 'gcc-multilib')
  provides=(lib32-gtk3{,-git}{,-ubuntu}="${pkgver}")
  conflicts=('lib32-gtk3')
  install=lib32-gtk3.install
  cd "gtk+/build-i686"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/{share,include}} # needs bin/

  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  find "$pkgdir/usr/bin" -type f -not -name gtk-query-immodules-3.0-32 -delete
}

