# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Antony Male <antony dot male at geemail dot com>>

pkgbase=snappy-git-multilib
pkgname=({lib32-,}snappy-git)
pkgver=r84.1ff9be9
pkgrel=1
pkgdesc='A fast compressor/decompressor library'
arch=('i686' 'x86_64')
url="http://code.google.com/p/snappy/"
license=('BSD')
#checkdepends=('zlib')
source=("git+https://github.com/google/snappy.git")
md5sums=('SKIP')

pkgver() {
  cd snappy
  echo $(git describe --long --tags | cut -d 'g' -f1)r$(git rev-list HEAD --count).$(git describe --always | sed 's/^.*-//1') | tr -d v | tr - .
}

build() {

  #Build 64bit libraries
  git clone "$srcdir/snappy" "$srcdir/snappy-build-x86_64"

  pushd "$srcdir/snappy-build-x86_64"

  # compile without assertions
  CXXFLAGS+=\ -DNDEBUG
  ./autogen.sh
  ./configure --prefix=/usr
  make

  popd

  #Build 32bit libraries
  git clone "$srcdir/snappy" "$srcdir/snappy-build-i686"

  pushd "$srcdir/snappy-build-i686"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS+=" -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  # compile without assertions
  CXXFLAGS+=\ -DNDEBUG
  ./autogen.sh
  ./configure --prefix=/usr --lib{,exec}dir=/usr/lib32 --includedir=/usr/lib32/snappy/include
  make

  popd
}

#check() {
#  # compile without assertions
#  CXXFLAGS+=\ -DNDEBUG
#
#  make -C "$pkgname" check
#}

package_snappy-git() {
  depends=('glibc' 'gcc-libs')
  provides=(snappy{,-git}=$pkgver)
  conflicts=(snappy)
  cd "snappy-build-x86_64"

  make DESTDIR="$pkgdir" install
  install -m644 -D COPYING "$pkgdir/usr/share/licenses/snappy/LICENSE"
}

package_lib32-snappy-git() {
  depends=(lib32-{glibc,gcc-libs})
  makedepends=('gcc-multilib')
  provides=(lib32-snappy{,-git}=$pkgver)
  conflicts=(lib32-snappy)
  cd "snappy-build-i686"

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/share
}


