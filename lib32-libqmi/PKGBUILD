# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libqmi
pkgver=1.10.0
pkgrel=1
pkgdesc="QMI modem protocol helper library (32bit)"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libqmi/"
license=(GPL2)
depends=(lib32-glib2 bash)
makedepends=(gtk-doc python)
source=(http://www.freedesktop.org/software/libqmi/libqmi-$pkgver.tar.xz)
sha256sums=('ea1a2389a7c790b54ed21a349d443d2e99ac02b8a05e20a41636e833f2bc805d')

build() {
  cd libqmi-$pkgver

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS+=" -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --with-tests --libexecdir=/usr/lib32 --libdir=/usr/lib32
  make
}

check() {
  cd libqmi-$pkgver
  make check
}

package() {
  cd libqmi-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin}}
}

