# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: feydaykyn <feydaykyn@yahoo.fr>

pkgbase=leveldb-git-multilib
pkgname=({lib32-,}leveldb-git)
pkgver=1.18.7.r89.77948e7
pkgrel=1
pkgdesc="A fast and lightweight key/value database library"
arch=('i686' 'x86_64')
url="https://code.google.com/p/leveldb/"
license=('BSD')
makedepends=('git')
source=("git+https://github.com/google/leveldb.git")
sha1sums=('SKIP')

_majorver="$(printf "$pkgver" | sed 's|\.[^\.]*||2g')"

pkgver() {
  cd leveldb
  echo $(git describe --long --tags | cut -d 'g' -f1)r$(git rev-list HEAD --count).$(git describe --always | sed 's/^.*-//1') | tr -d v | tr - .
}


build() {

  #Build 64bit libraries
  git clone "$srcdir/leveldb" "$srcdir/leveldb-build-x86_64"

  make -C "leveldb-build-x86_64"

  #Build 32bit libraries
  git clone "$srcdir/leveldb" "$srcdir/leveldb-build-i686"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  make -C "leveldb-build-i686"

}

#check() {
#  make -C "$pkgname" check
#}

package_leveldb-git() {
  depends=('gperftools' 'snappy')
  provides=(leveldb{,-git}=$pkgver)
  conflicts=('leveldb')
  cd "leveldb-build-x86_64"

  install -dm755 \
      "$pkgdir"/usr/{include/leveldb,lib} \
      "$pkgdir"/usr/share/doc/leveldb

  # libraries
  install -m644 -t "$pkgdir/usr/lib" "libleveldb.so.$_majorver" libleveldb.a
  cp -P libleveldb.so* "$pkgdir/usr/lib"

  # headers
  install -m644 -t "$pkgdir/usr/include/leveldb" include/leveldb/*

  # documentation
  cp -r doc/* "$pkgdir/usr/share/doc/leveldb"

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/leveldb/LICENSE"
}

package_lib32-leveldb-git() {
  pkgdesc+=" (32bit)"
  depends=('lib32-gperftools' 'lib32-snappy')
  provides=(lib32-leveldb{,-git}=$pkgver)
  conflicts=('lib32-leveldb')
  cd "leveldb-build-i686"

  install -dm755 \
      "$pkgdir"/usr/lib32/leveldb{,/include}

  # libraries
  install -m644 -t "$pkgdir/usr/lib32" "libleveldb.so.$_majorver" libleveldb.a
  cp -P libleveldb.so* "$pkgdir/usr/lib32"

  # headers
  install -m644 -t "$pkgdir/usr/lib32/leveldb/include" include/leveldb/*

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/lib32-leveldb/LICENSE"
}

