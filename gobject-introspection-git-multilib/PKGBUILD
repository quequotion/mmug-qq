# Maintainer: Joao Cordeiro <jlcordeiro at gmail dot com>
# Contributor: <arsenm2@rpi.edu>

pkgbase="gobject-introspection-git-multilib"
pkgname=("gobject-introspection-git" "lib32-gobject-introspection-git")
pkgver=1.41.4.22.gadf3ada
pkgrel=1
pkgdesc="GObject Introspection (git version)"
epoch=1
arch=('x86_64' 'i686')
license=('LGPL' 'GPL')
url="http://live.gnome.org/GObjectIntrospection/"
options=(!makeflags docs !libtool strip debug)
source=("git://git.gnome.org/gobject-introspection"
        'lib32-pkgconfig.patch')
sha512sums=('SKIP'
            'f9410c20cc04b2f4911f483bed8cf1c58a14095c732d71d520a83051039c678824031dddf5ad38db28a865ab9eaec0fdcdf20aba464029e6ea4f7d31ce0d6fe0')

pkgver() {
    cd gobject-introspection/

    git describe --always | sed "s/[B-T]//g;s/_//;s/[E-T]//g;s/_//;s/_/./g;s/-/./g"
}

build() {
  cd "$srcdir/gobject-introspection"

  #Build 64bit libraries
  [[ -d build-x86_64 ]] || mkdir build-x86_64
  pushd build-x86_64

  export PYTHON=/usr/bin/python2
  ../autogen.sh --prefix=/usr --disable-static --enable-doctool

  make

  popd

  #Build 32bit libraries
  patch -Np2 < ../lib32-pkgconfig.patch

  [[ -d build-i686 ]] || mkdir build-i686
  pushd build-i686

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  export PYTHON=/usr/bin/python2.7-32
  export PYTHON_LIBS=/usr/lib32/python2.7
  export pyexecdir=/usr/lib32/python2.7/site-packages
  export pythondir=/usr/lib32/python2.7/site-packages

  ../autogen.sh --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
                --program-suffix=-32 --includedir=/usr/lib32/gobject-introspection/include \
                --disable-static --enable-doctool \
                --with-python=/usr/bin/python2.7-32

  make

  popd
}

package_gobject-introspection-git() {
  depends=("glib2-git" 'python2' 'python2-mako')
  makedepends=('git' 'pkgconfig' 'autoconf' 'gtk-doc' 'gnome-common-git'
               'bison' 'cairo' 'mesa' 'flex')
  provides=("gobject-introspection=${pkgver}")
  conflicts=('gobject-introspection' 'gobject-introspection-svn')
  replaces=('gobject-introspection-svn')

  cd "$srcdir/gobject-introspection/build-x86_64"
  make DESTDIR="$pkgdir" install

  sed -i '1s|#!/usr/bin/env python$|&2|' \
    "$pkgdir"/usr/lib/gobject-introspection/giscanner/*.py
}

package_lib32-gobject-introspection-git() {
  pkgdesc+=" (32bit)"
  depends=("lib32-glib2-git" 'lib32-python2' 'python2-mako')
  makedepends=('git' 'pkgconfig' 'autoconf' 'gtk-doc' 'gnome-common-git'
               'bison' 'lib32-cairo' 'lib32-mesa' 'lib32-flex')
  provides=("lib32-gobject-introspection=${pkgver}")
  conflicts=('lib32-gobject-introspection' 'lib32-gobject-introspection-svn')
  replaces=('lib32-gobject-introspection-svn')

  cd "$srcdir/gobject-introspection/build-i686"
  make DESTDIR="$pkgdir" install

  sed -i '1s|#!/usr/bin/env python$|&2|' \
    "$pkgdir"/usr/lib32/gobject-introspection/giscanner/*.py

  rm -rf "${pkgdir}"/{etc,usr/{share,include}} # needs bin/
}
