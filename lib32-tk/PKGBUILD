# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=lib32-tk
pkgver=8.6.3
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl (32bit)"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=(lib32-{"tcl=${pkgver}",lib{xss,xft}})
provides=("lib32-tk=${pkgver}")
options=('staticlibs')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
sha1sums=('ecfcc20833c04d6890b14a7920a04d16f2123a51')

build() {
  cd tk${pkgver}/unix

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr --lib{,exec}dir=/usr/lib32 \
              --mandir=/usr/share/man --enable-threads --disable-rpath
  make
}

check() {
  cd tk${pkgver}/unix
#  make test
}

package() {
  cd tk${pkgver}/unix
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf wish${pkgver%.*} "${pkgdir}/usr/bin/wish"
  ln -sf libtk${pkgver%.*}.so "${pkgdir}/usr/lib32/libtk.so"

  # remove buildroot traces
  sed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib32#" \
      -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
      -i "${pkgdir}/usr/lib32/tkConfig.sh"

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin,lib}} #cruft in lib/

  install -Dm644 license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

