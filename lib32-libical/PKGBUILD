# Maintainter: quequotion <quequotion@bugmenot.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=lib32-libical
pkgver=1.0
pkgrel=3
pkgdesc="An open source reference implementation of the icalendar data type and serialization format (32bit)"
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/freeassociation/'
license=('LGPL' 'MPL')
depends=('lib32-glibc')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/freeassociation/libical-${pkgver}.tar.gz")
md5sums=('4438c31d00ec434f02867a267a92f8a1')

prepare() {
  mkdir build
}

build() {
  cd build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cmake ../libical-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_SUFFIX=32

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/{share,include,bin}}
}

